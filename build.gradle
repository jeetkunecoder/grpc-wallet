plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.8'
    id 'idea'
}

repositories {
    maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.19.0'
def protobufVersion = '3.7.1'
def protocVersion = protobufVersion
def springBootVersion = '2.1.4.RELEASE'

dependencies {
    implementation "io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE"
    implementation "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'io.github.lognet:grpc-spring-boot-starter:3.2.1'
    implementation 'org.postgresql:postgresql:42.2.5'
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation 'commons-cli:commons-cli:1.4'

    compileOnly "javax.annotation:javax.annotation-api:1.2"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task walletServer(type: CreateStartScripts) {
    mainClassName = 'com.betpawa.wallet.WalletApplication'
    applicationName = 'wallet-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task walletClient(type: CreateStartScripts) {
    mainClassName = 'com.betpawa.wallet.WalletClient'
    applicationName = 'wallet-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(walletServer)
    from(walletClient)
    fileMode = 0755
}